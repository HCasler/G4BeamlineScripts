####################################
#
# Extinction monitor detector room
#
####################################
param bend=$EMFilter_Bdl*0.2998/$EMFilter_p
param detBend=$EMDetector_Bdl*0.2998/$EMFilter_p
param au=$EMFilter_pitch-$bend
param ad=$EMFilter_pitch-$bend-$detBend
param phi=$pBeamStop_yrot*degree
param yaw=$EMFilter_yaw-($pBeamStop_yrot-$PBSenclosure_yrot)*degree

#Calculate location of room centroid
param xr=$PBSenclosure_x
param yr=$PBSenclosure_y
param zr=$PBSenclosure_z

param xu=$EMDetector_xstart
# Create detector planes
param ux=($EMDetector_xstart-$xr)*cos($phi)-($EMDetector_zstart-$zr)*sin($phi)
param uy=$EMDetector_ystart-$yr
param uz=($EMDetector_zstart-$zr)*cos($phi)+($EMDetector_xstart-$xr)*sin($phi)
param dx=($EMDetector_xend-$xr)*cos($phi)-($EMDetector_zend-$zr)*sin($phi)
param dy=$EMDetector_yend-$yr
param dz=($EMDetector_zend-$zr)*cos($phi)+($EMDetector_xend-$xr)*sin($phi)
if $ScintDetector==1
  virtualdetector scintu color=$Green material=PLASTIC_SC_VINYLTOLUENE \
      length=$EMDetector_lc width=$EMDetector_uw height=$EMDetector_uh
  virtualdetector scintd color=$Green material=PLASTIC_SC_VINYLTOLUENE \
      length=$EMDetector_lc width=$EMDetector_dw height=$EMDetector_dh
else
  box scintu color=$Green material=PLASTIC_SC_VINYLTOLUENE \
      length=$EMDetector_lc width=$EMDetector_uw height=$EMDetector_uh
  box scintd color=$Green material=PLASTIC_SC_VINYLTOLUENE \
      length=$EMDetector_lc width=$EMDetector_dw height=$EMDetector_dh
endif
place scintu rename=+scint1 parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
    x=$ux y=$uy z=$uz
place scintd rename=+scint6 parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
    x=$dx y=$dy z=$dz
param dl=$EMDetector_sc
place scintu rename=+scint2 parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
    x=$ux+$dl*cos($au)*sin($yaw) y=$uy+$dl*sin($au) z=$uz-$dl*cos($au)*cos($yaw)
place scintd rename=+scint5 parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
    x=$dx-$dl*cos($ad)*sin($yaw) y=$dy-$dl*sin($ad) z=$dz+$dl*cos($ad)*cos($yaw)
param dl=3*$EMDetector_sc+($EMDetector_planes-1)*$EMDetector_sd
place scintu rename=+scint3 parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
    x=$ux+$dl*cos($au)*sin($yaw) y=$uy+$dl*sin($au) z=$uz-$dl*cos($au)*cos($yaw)
place scintd rename=+scint4 parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
    x=$dx-$dl*cos($ad)*sin($yaw) y=$dy-$dl*sin($ad) z=$dz+$dl*cos($ad)*cos($yaw)
param dl=2*$EMDetector_sc
do i 1 $EMDetector_planes
  if $PixelDetector==1
    virtualdetector plane$i color=$Red material=Si \
	length=$EMDetector_l width=$EMDetector_uw height=$EMDetector_uh
  else
    box plane$i color=$Red material=Si \
	length=$EMDetector_l width=$EMDetector_uw height=$EMDetector_uh
  endif
  place plane$i parent=PBSenc rotation=x$au/degree,y-$yaw/degree \
  	x=$ux+$dl*cos($au)*sin($yaw) \
	y=$uy+$dl*sin($au) \
	z=$uz-$dl*cos($au)*cos($yaw)
  param j=1+2*$EMDetector_planes-$i
  if $PixelDetector==1
    virtualdetector plane$j color=$Red material=Si \
	length=$EMDetector_l width=$EMDetector_dw height=$EMDetector_dh
  else
    box plane$j color=$Red material=Si \
	length=$EMDetector_l width=$EMDetector_dw height=$EMDetector_dh
  endif
  place plane$j parent=PBSenc rotation=x$ad/degree,y-$yaw/degree \
  	x=$dx-$dl*cos($ad)*sin($yaw) \
	y=$dy-$dl*sin($ad) \
	z=$dz+$dl*cos($ad)*cos($yaw)
  param dl=$dl+$EMDetector_sd
enddo

# Create spectrometer magnet
genericbend detMag ironColor=$Teal By=$EMDetector_B \
	ironMaterial=Fe fieldMaterial=$TargetHall_material \
	ironWidth=$EMDetector_hmagnet  fieldWidth=$EMDetector_hgap \
	ironHeight=$EMDetector_wmagnet fieldHeight=$EMDetector_wgap \
	ironLength=$EMDetector_lmagnet fieldLength=$EMDetector_lgap

param Lm=($EMDetector_planes-1)*$EMDetector_sd+$EMDetector_sm
param zrot=-90
if $EMDetector_Bdl<0
  param zrot=90
endif
place detMag parent=PBSenc rotation=z$zrot,x($ad+$au)/(2*degree),y-$yaw/degree \
	x=($ux-$Lm*cos($au)*sin($yaw)+$dx+$Lm*cos($ad)*sin($yaw))/2 \
	y=($uy+$Lm*sin($au)+$dy-$Lm*sin($ad))/2 \
	z=($uz-$Lm*cos($au)*cos($yaw)+$dz+$Lm*cos($ad)*cos($yaw))/2

