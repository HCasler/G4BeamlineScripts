# **************************************************************
#   PS crystat, coil support, enclosure and heat shield 
# **************************************************************
param CL1_inner_r=$PSshield_r2
param pi=acos(-1.0)
param x=$PScryostat_x
param y=$PScryostat_y
param z=$PScryostat_z+$PScryostat_Offset+$PScryostat_length/2

param -unset PSCinvisPhi=-30
param -unset PSEinvisPhi=-90
param -unset HRSinvisPhi=-90
param -unset Aluminum="0.66,0.67,0.71"
param -unset Titanium="0.55,0.57,0.60"

# **************************************************************
# cryostat
# **************************************************************
tubs PSCryoOuterV outerRadius=$PScryostat_R4 innerRadius=$PScryostat_R3 \
	initialPhi=$PSCinvisPhi+180 finalPhi=$PSCinvisPhi+360 \
	length=$PScryostat_length material=$PScryostat_material color=$Aluminum
tubs PSCryoOuterI outerRadius=$PScryostat_R4 innerRadius=$PScryostat_R3 \
	initialPhi=$PSCinvisPhi finalPhi=$PSCinvisPhi+180 \
	length=$PScryostat_length material=$PScryostat_material color=$PSCinvisCol
tubs PSCryoInnerV outerRadius=$PScryostat_R2 innerRadius=$PScryostat_R1 \
	initialPhi=$PSCinvisPhi+180 finalPhi=$PSCinvisPhi+360 \
	length=$PScryostat_length material=$PScryostat_material color=$Aluminum
tubs PSCryoInnerI outerRadius=$PScryostat_R2 innerRadius=$PScryostat_R1 \
	initialPhi=$PSCinvisPhi finalPhi=$PSCinvisPhi+180 \
	length=$PScryostat_length material=$PScryostat_material color=$PSCinvisCol
tubs PSCryoEndV outerRadius=$PScryostat_R3 innerRadius=$PScryostat_R2 \
	initialPhi=$PSCinvisPhi+180 finalPhi=$PSCinvisPhi+360 \
	length=$PScryostat_thick material=$PScryostat_material color=$Aluminum
tubs PSCryoEndI outerRadius=$PScryostat_R3 innerRadius=$PScryostat_R2 \
	initialPhi=$PSCinvisPhi finalPhi=$PSCinvisPhi+180 \
	length=$PScryostat_thick material=$PScryostat_material color=$PSCinvisCol
place PSCryoOuterV x=$x y=$y z=$z kill=$PScryostatKill
place PSCryoOuterI x=$x y=$y z=$z kill=$PScryostatKill
place PSCryoInnerV x=$x y=$y z=$z kill=$PScryostatKill
place PSCryoInnerI x=$x y=$y z=$z kill=$PScryostatKill
place PSCryoEndV x=$x y=$y z=$z+($PScryostat_length-$PScryostat_thick)/2 kill=$PScryostatKill
place PSCryoEndV x=$x y=$y z=$z-($PScryostat_length-$PScryostat_thick)/2 kill=$PScryostatKill
place PSCryoEndI x=$x y=$y z=$z+($PScryostat_length-$PScryostat_thick)/2 kill=$PScryostatKill
place PSCryoEndI x=$x y=$y z=$z-($PScryostat_length-$PScryostat_thick)/2 kill=$PScryostatKill

# **************************************************************
# End Cap
# **************************************************************
param ir=$PSendcap_OuterR-$PSendcap_wall

tubs PSEshellV outerRadius=$PSendcap_OuterR innerRadius=$ir \
	initialPhi=$PSEinvisPhi+180 finalPhi=$PSEinvisPhi+360 \
	length=$PSendcap_length material=$PSendcap_material color=$Aluminum
tubs PSEfaceV outerRadius=$ir innerRadius=0.0 length=$PSendcap_thick \
	initialPhi=$PSEinvisPhi+180 finalPhi=$PSEinvisPhi+360 \
	material=$PSendcap_material color=$Aluminum,0.9
tubs PSEwin1v outerRadius=$PSendcap_winR1 innerRadius=0.0 \
	length=$PSendcap_thick material=Vacuum
tubs PSEwin1vs outerRadius=$PSendcap_winR1 innerRadius=0.0 \
	length=0.2 material=Vacuum color=$Titanium
tubs PSEwin1m outerRadius=$PSendcap_winR1+$PSendcap_winT innerRadius=0.0 \
	length=$PSendcap_winT material=$PSendcap_winM color=$Titanium
tubs PSEwin2v outerRadius=$PSendcap_winR2 innerRadius=0.0 \
	length=$PSendcap_thick material=Vacuum
tubs PSEwin2vs outerRadius=$PSendcap_winR2 innerRadius=0.0 \
	length=0.2 material=Vacuum color=$Titanium
tubs PSEwin2m outerRadius=$PSendcap_winR2+$PSendcap_winT innerRadius=0.0 \
	length=$PSendcap_winT material=$PSendcap_winM color=$Titanium

place PSEwin1v parent=PSEfaceV x=$PSendcap_winX1 y=$PSendcap_winY1 z=0
place PSEwin2v parent=PSEfaceV x=$PSendcap_winX2 y=$PSendcap_winY2 z=0

tubs PSEshellI outerRadius=$PSendcap_OuterR innerRadius=$ir \
	initialPhi=$PSEinvisPhi finalPhi=$PSEinvisPhi+180 \
	length=$PSendcap_length material=$PSendcap_material color=$PSEinvisCol
tubs PSEfaceI outerRadius=$ir innerRadius=0.0 length=$PSendcap_thick \
	initialPhi=$PSEinvisPhi finalPhi=$PSEinvisPhi+180 \
	material=$PSendcap_material color=$PSEinvisCol
param z=$PScryostat_z+$PScryostat_Offset
place PSEfaceV x=$x y=$y z=$z-$PSendcap_length+$PSendcap_thick/2
place PSEshellV x=$x y=$y z=$z-$PSendcap_length/2
place PSEwin1m x=$PSendcap_winX1 y=$PSendcap_winY1 z=$z-$PSendcap_length-$PSendcap_winT/2
place PSEwin1vs x=$PSendcap_winX1 y=$PSendcap_winY1 z=$z-$PSendcap_length+$PSendcap_thick+0.1
place PSEwin2m x=$PSendcap_winX2 y=$PSendcap_winY2 z=$z-$PSendcap_length-$PSendcap_winT/2
place PSEwin2vs x=$PSendcap_winX2 y=$PSendcap_winY2 z=$z-$PSendcap_length+$PSendcap_thick+0.1
place PSEfaceI x=$x y=$y z=$z-$PSendcap_length+$PSendcap_thick/2
place PSEshellI x=$x y=$y z=$z-$PSendcap_length/2

# **************************************************************
# coil support
# **************************************************************
param len1=$PScoils_D1+$PScoils_L1+$PScoils_D2
param z1=$PScoils_D1
param z2=$z1+$PScoils_L1
param z3=$z2+$PScoils_D2
param z4=$z3+$PScoils_L2
param z5=$z4+$PScoils_D3
param z6=$z5+$PScoils_L3
param z7=$z6+$PScoils_D4
param r1=$PScoils_R0
param r2=$PScoils_R1a
param r3=$PScoils_R0
param r4=$PScoils_R23a
param r5=$PScoils_R0
param r6=$PScoils_R23a
param r7=$PScoils_R0
param R1=$PScoils_R1b
param R2=$PScoils_R1b
param R3=$PScoils_R1b
param R4=$PScoils_R23b
param R5=$PScoils_R23b
param R6=$PScoils_R23b
param R7=$PScoils_R23b
polycone PSCoilsupV material=$PScoils_material color=$Aluminum \
	initialPhi=$PSCinvisPhi+180 finalPhi=$PSCinvisPhi+360 \
	outerRadius=$R1,$R1,$R2,$R2,$R3,$R3,$R4,$R4,$R5,$R5,$R6,$R6,$R7,$R7 \
	innerRadius=$r1,$r1,$r2,$r2,$r3,$r3,$r4,$r4,$r5,$r5,$r6,$r6,$r7,$r7 \
	z=0,$z1,$z1,$z2,$z2,$z3,$z3,$z4,$z4,$z5,$z5,$z6,$z6,$z7
polycone PSCoilsupI material=$PScoils_material color=$PSSinvisCol \
	initialPhi=$PSCinvisPhi finalPhi=$PSCinvisPhi+180 \
	outerRadius=$R1,$R1,$R2,$R2,$R3,$R3,$R4,$R4,$R5,$R5,$R6,$R6,$R7,$R7 \
	innerRadius=$r1,$r1,$r2,$r2,$r3,$r3,$r4,$r4,$r5,$r5,$r6,$r6,$r7,$r7 \
	z=0,$z1,$z1,$z2,$z2,$z3,$z3,$z4,$z4,$z5,$z5,$z6,$z6,$z7

place PSCoilsupV x=$x y=$y z=$PScryostat_z+$PScoils_Offset
place PSCoilsupI x=$x y=$y z=$PScryostat_z+$PScoils_Offset

# **************************************************************
# heat shield
# **************************************************************
param R0=$PSshield_r0
param Rx1=$PSshield_r1
param Rx2=$PSshield_r2
param Rx3=$PSshield_r3
param Rx4=$PSshield_r4
param Rx5=$PSshield_r5
param Rx6=$PSshield_r6
param Rx7=$PSshield_r7
param Z0=$PSshield_z0
param Zx1=$PSshield_z1
param Zx2=$PSshield_z2
param Zx3=$PSshield_z3
param Zx4=$PSshield_z4
param Zx5=$PSshield_z5
param Zx6=$PSshield_z6
param Zx7=$PSshield_z7
param rout=$PSshield_out
param radd=$PSshield_Radd
param ra=$PSshield_radd
param za=$PSshield_zadd
param m=$PSshield_material

param amin=$HRSinvisPhi+270.0
param amax=$HRSinvisPhi+270.0

if $PSshield_Grooves>0
  include ./Geometry/HRSGrooves.txt
endif

if $PSshield_reduced==1&&$PSshield_Grooves==0

  polycone PSshieldV1 material=$m color=$Copper \
	initialPhi=$HRSinvisPhi+180 finalPhi=$HRSinvisPhi+360 \
	innerRadius=$R0,$Rx1,$ra,$ra,$Rx2,$Rx3 \
 	outerRadius=$radd,$radd,$radd,$rout,$rout,$rout \
	z=$Z0,$Zx1,$za,$za,$Zx2,$Zx3

  polycone PSshieldI material=$m color=$HRSinvisCol \
	initialPhi=$HRSinvisPhi finalPhi=$HRSinvisPhi+180 \
	innerRadius=$R0,$Rx1,$ra,$ra,$Rx2,$Rx3 \
 	outerRadius=$radd,$radd,$radd,$rout,$rout,$rout \
	z=$Z0,$Zx1,$za,$za,$Zx2,$Zx3
	
  polycone PSshieldV2 material=$m color=$Copper \
	initialPhi=0 finalPhi=360 \
	innerRadius=$Rx4,$Rx5 \
 	outerRadius=$Rx6,$Rx7 \
	z=$Zx4,$Zx5

    place PSshieldV1 x=$x y=$y z=$PScryostat_z+$PSshield_Offset kill=$PSshieldKill
    place PSshieldV2 x=$x y=$y z=$PScryostat_z+$PSshield_Offset kill=$PSshieldKill
    place PSshieldI x=$x y=$y z=$PScryostat_z+$PSshield_Offset kill=$PSshieldKill

elseif $PSshield_new==1&&$PSshield_Grooves==0 
    
  polycone HRS_St_S \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r0,$PSshield_r1,$PSshield_r1,$PSshield_r1,$PSshield_r1 \
    outerRadius=$PSshield_r2,$PSshield_r3,$PSshield_r3,$PSshield_r3,$PSshield_r3 \
    initialPhi=90 \
    finalPhi=270 \
    material=Stainless316 \
    color="0,0,1"

  polycone HRS_St_S_invis \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r0,$PSshield_r1,$PSshield_r1,$PSshield_r1,$PSshield_r1 \
    outerRadius=$PSshield_r2,$PSshield_r3,$PSshield_r3,$PSshield_r3,$PSshield_r3 \
    initialPhi=-90 \
    finalPhi=90 \
    material=Stainless316 \
    color=invisible

  place HRS_St_S \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  place HRS_St_S_invis \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  polycone HRS_Water_in \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r2,$PSshield_r3,$PSshield_r3,$PSshield_r3,$PSshield_r3 \
    outerRadius=$PSshield_r4,$PSshield_r5,$PSshield_r5,$PSshield_r5,$PSshield_r5 \
    initialPhi=90 \
    finalPhi=270 \
    material=WATER \
    color="0,0,0"

  polycone HRS_Water_in_invis \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r2,$PSshield_r3,$PSshield_r3,$PSshield_r3,$PSshield_r3 \
    outerRadius=$PSshield_r4,$PSshield_r5,$PSshield_r5,$PSshield_r5,$PSshield_r5 \
    initialPhi=-90 \
    finalPhi=90 \
    material=WATER \
    color=invisible

  place HRS_Water_in \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  place HRS_Water_in_invis \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  polycone HRS_Bronze \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r4,$PSshield_r5,$PSshield_r5,$PSshield_r5,$PSshield_r5 \
    outerRadius=$PSshield_r6,$PSshield_r6,$PSshield_r6,$PSshield_r7,$PSshield_r7 \
    initialPhi=90 \
    finalPhi=270 \
    material=Bronze \
    color=".5,.5,.5"

  polycone HRS_Bronze_invis \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r4,$PSshield_r5,$PSshield_r5,$PSshield_r5,$PSshield_r5 \
    outerRadius=$PSshield_r6,$PSshield_r6,$PSshield_r6,$PSshield_r7,$PSshield_r7 \
    initialPhi=-90 \
    finalPhi=90 \
    material=Bronze \
    color=invisible

  place HRS_Bronze \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  place HRS_Bronze_invis \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  polycone HRS_Water_out \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r6,$PSshield_r6,$PSshield_r6,$PSshield_r7,$PSshield_r7 \
    outerRadius=750.0,750.0,750.0,750.0,750.0 \
    initialPhi=90 \
    finalPhi=270 \
    material=WATER \
    color="0,0.8,0"

  polycone HRS_Water_out_invis \
    z=$PSshield_z0,$PSshield_z1,$PSshield_z2,$PSshield_z3,$PSshield_z4 \
    innerRadius=$PSshield_r6,$PSshield_r6,$PSshield_r6,$PSshield_r7,$PSshield_r7 \
    outerRadius=750.0,750.0,750.0,750.0,750.0 \
    initialPhi=-90 \
    finalPhi=90 \
    material=WATER \
    color=invisible

  place HRS_Water_out \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

  place HRS_Water_out_invis \
    x=0.0 \
    y=0.0 \
    z=$G4bl_Mars_z

else

  polycone PSshieldV1 material=$m color=$Copper \
	initialPhi=$HRSinvisPhi+180 finalPhi=$amin \
	innerRadius=$R0,$Rx1,$Rx2,$Rx3,$Rx4,$Rx5,$Rx6,$Rx7 \
 	outerRadius=$rout,$rout,$rout,$rout,$rout,$rout,$rout,$rout \
	z=$Z0,$Zx1,$Zx2,$Zx3,$Zx4,$Zx5,$Zx6,$Zx7
  polycone PSshieldV2 material=$m color=$Copper \
	initialPhi=$amax finalPhi=$HRSinvisPhi+360 \
	innerRadius=$R0,$Rx1,$Rx2,$Rx3,$Rx4,$Rx5,$Rx6,$Rx7 \
 	outerRadius=$rout,$rout,$rout,$rout,$rout,$rout,$rout,$rout \
	z=$Z0,$Zx1,$Zx2,$Zx3,$Zx4,$Zx5,$Zx6,$Zx7
  polycone PSshieldI material=$m color=$HRSinvisCol \
	initialPhi=$HRSinvisPhi finalPhi=$HRSinvisPhi+180 \
	innerRadius=$R0,$Rx1,$Rx2,$Rx3,$Rx4,$Rx5,$Rx6,$Rx7 \
 	outerRadius=$rout,$rout,$rout,$rout,$rout,$rout,$rout,$rout \
	z=$Z0,$Zx1,$Zx2,$Zx3,$Zx4,$Zx5,$Zx6,$Zx7

    place PSshieldV1 x=$x y=$y z=$PScryostat_z+$PSshield_Offset kill=$PSshieldKill
    place PSshieldV2 x=$x y=$y z=$PScryostat_z+$PSshield_Offset kill=$PSshieldKill
    place PSshieldI x=$x y=$y z=$PScryostat_z+$PSshield_Offset kill=$PSshieldKill
endif


# **************************************************************
# Beam entry pipe
# **************************************************************

param dz=$ZbeamD
param dx=$XbeamD-$XbeamU
param dy=$YbeamD-$YbeamU
param dr=$RbeamD-$RbeamU
param ZpipeU=$Rpipe*$dr/$dz
param ZpipeD=$ZbeamD-$Rpipe*$dz/$dr
param RpipeU=$RbeamU+$ZpipeU*$dr/$dz
param RpipeD=$RbeamU+$ZpipeD*$dr/$dz
param Lpipe=sqrt(($RpipeU-$RpipeD)*($RpipeU-$RpipeD)+($ZpipeU-$ZpipeD)*($ZpipeU-$ZpipeD))
tubs PSpipe length=$Lpipe outerRadius=$Rpipe innerRadius=0 \
	material=Vacuum color=1,1,1,0.7
param Zpipe=($ZpipeU+$ZpipeD)/2
param Xpipe=$XbeamU+$Zpipe*$dx/$dz
param Ypipe=$YbeamU+$Zpipe*$dy/$dz
param theta=atan(sqrt($dx*$dx+$dy*$dy)/$ZbeamD)*180/$pi
param phi=atan2($dy,$dx)*180/$pi
if $PSshield_Entrance==1
  place PSpipe x=$Xpipe y=$Ypipe z=$PScryostat_z+$PSshield_Offset+$Zx7+$Zpipe \
	rotation=y$theta,z$phi
endif
