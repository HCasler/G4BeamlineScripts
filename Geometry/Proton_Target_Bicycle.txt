# $Id$
# $Date$
# $Author$

# ****************************************************************************
#                  "Bicycle Wheel" model of Proton Target
# ****************************************************************************

param Tposition=-6564.5+$MECO_G4_zTrans+400.
param Tlength=160.0 Tradius=3.0
param TYangle=13.65
param TXangle=0.0
param -unset Target_Color=$Gold
param -unset Target_Shell_Color=$Gray

##********************
## Golden target parameters

   param target_or1=3.896*.5
   param target_or2=6.299*.5
   param target_ir=0.0
   param target_l=160.0
   param target_z1=0.0
   param target_z2=10.0
   param target_z3=$target_l-$target_z2

##********************
## Target shell dimensions

param t_shell_r1=6.911*.5
param t_shell_r2=7.927*.5
param t_shell_l=152.228
param t_shell_mat=Ti

##********************
## Front Target shell caps dimetsions
## Cap1 (front)

param cap1_r1=$t_shell_r2
param cap1_r2=(7.927+9.525)*.25
param cap1_l=2.464
param cap1_z=.5*($t_shell_l-$cap1_l)

##********************
## Cap2 (front)

param cap2_r1=$t_shell_r1
param cap2_r2=$cap1_r2
param cap2_l=3.175-$cap1_l
param cap2_z=$cap1_z+.5*($cap1_l+$cap2_l)

##********************
## Cap3 (front)

param cap3_r1=$t_shell_r1
param cap3_r2=9.525*.5
param cap3_l=7.925-3.175
param cap3_z=$cap2_z+.5*($cap2_l+$cap3_l)

##********************
## Cap4 (front)

param cap4_r1=0.0
param cap4_r2=$cap3_r1
param cap4_l=1.575-.6
param cap4_z=$cap3_z+.5*($cap3_l-$cap4_l)

##********************
## Cap5 (front)

param cap5_r1=0.0
param cap5_r2=1.4*.5
param cap5_l=$cap3_r2-$cap3_r1
param cap5_z=.5*$cap3_l-$cap4_l-$cap5_r2
param cap5_y=$cap3_r2-.5*$cap5_l

##********************
## Target shell rear caps dimensions 
## Cap1 (rear)

param cap1_z_rear=-.5*($t_shell_l-$cap1_l)

##********************
## Cap2 (rear)

param cap2_z_rear=$cap1_z_rear-.5*($cap1_l+$cap2_l)

##********************
## Cap3 (rear)

param cap3_z_rear=$cap2_z_rear-.5*($cap2_l+$cap3_l)

##********************
## Cap4 (rear)q

param cap4_l_rear=$cap4_l+.6
param cap4_z_rear=$cap3_z_rear-.5*($cap3_l-$cap4_l_rear)

##********************
## Cap5 (raer)

param cap5_z_rear=-.5*$cap3_l+$cap4_l_rear+$cap5_r2

param target_center=$cap2_l+$cap3_l+.5*$t_shell_l

##********************
## Group all target shell parts and golden target into 
## one single object.

group proton_target

##********************
## Golden Target
      polycone target \
               outerRadius=$target_or2,$target_or2,$target_or1 \
               z=$target_z1,$target_z3,$target_l \
               material=Au \
               color=$Target_Color
	  
      place target \
            x=0 \
            y=0 \
            z=$target_center-.5*$target_l

##********************
## Target Shell

      tubs t_shell \
           innerRadius=$t_shell_r1 \
           outerRadius=$t_shell_r2 \
           length=$t_shell_l \
           material=$t_shell_mat \
           color=$Target_Shell_Color
     
      place t_shell \
            x=0 \
            y=0 \
            z=$target_center

##********************
## Front Target Shell Caps

      tubs cap1 \
           innerRadius=$cap1_r1 \
           outerRadius=$cap1_r2 \
           length=$cap1_l \
           material=$t_shell_mat \
           color=$Target_Shell_Color \

      place cap1 \
            x=0 \
            y=0 \
            z=$target_center+$cap1_z

      tubs cap2 \
           innerRadius=$cap2_r1 \
           outerRadius=$cap2_r2 \
           length=$cap2_l \
           material=$t_shell_mat \
           color=$Target_Shell_Color \

      place cap2 \
            x=0 \
            y=0 \
            z=$target_center+$cap2_z

      tubs cap3 \
           innerRadius=$cap3_r1 \
           outerRadius=$cap3_r2 \
           length=$cap3_l \
           material=$t_shell_mat \
           color=$Target_Shell_Color \

      tubs cap4 \
           innerRadius=$cap4_r1 \
           outerRadius=$cap4_r2 \
           length=$cap4_l \
           material=$t_shell_mat \
           color=$Target_Shell_Color \
      
      place cap4 \
            x=0 \
            y=0 \
            z=$target_center+$cap4_z

      tubs cap5 \
           innerRadius=$cap5_r1 \
           outerRadius=$cap5_r2 \
           length=$cap5_l \
           material=Vacuum \
           color=$Red 

      place cap5 \
            x=$cap5_y*sin(3.14159/6) \
            y=$cap5_y*cos(3.14159/6) \
            z=$cap5_z \
            parent=cap3 \
            rotation=X90,Z-30.003 \
	    rename=cap5_1

      place cap5 \
            x=$cap5_y*sin(3.14159/6) \
            y=-$cap5_y*cos(3.14159/6) \
            z=$cap5_z \
            parent=cap3 \
            rotation=X90,Z-150.003 \
	    rename=cap5_2

      place cap5 \
            x=-$cap5_y \
            y=0 \
            z=$cap5_z \
            parent=cap3 \
            rotation=X90,Z-270.003 \
	    rename=cap5_3
	        
      place cap3 \
            x=0 \
            y=0 \
            z=$target_center+$cap3_z
      
##********************
## Rear Target Shell Caps

      place cap1 \
            x=0 \
            y=0 \
            z=$target_center+$cap1_z_rear
      
      place cap2 \
            x=0 \
            y=0 \
            z=$target_center+$cap2_z_rear

      tubs cap3_rear \
           innerRadius=$cap3_r1 \
           outerRadius=$cap3_r2 \
           length=$cap3_l \
           material=$t_shell_mat \
           color=$Target_Shell_Color 

      tubs cap5_rear \
           innerRadius=$cap5_r1 \
           outerRadius=$cap5_r2 \
           length=$cap5_l \
           material=Vacuum \
           color=$Red 

      place cap5_rear \
            x=-$cap5_y*sin(3.14159/6) \
            y=-$cap5_y*cos(3.14159/6) \
            z=$cap5_z_rear \
            parent=cap3_rear \
            rotation=X90,Z-210.003 \
	    rename=cap5_rear1
	   
      place cap5_rear \
            x=-$cap5_y*sin(3.14159/6) \
            y=$cap5_y*cos(3.14159/6) \
            z=$cap5_z_rear \
            parent=cap3_rear \
            rotation=X90,Z59.997 \
	    rename=cap5_rear2

      place cap5_rear \
            x=$cap5_y \
            y=0 \
            z=$cap5_z_rear \
            parent=cap3_rear \
            rotation=X90,Z90.003 \
	    rename=cap5_rear3
	    	    
      place cap3_rear \
            x=0 \
            y=0 \
            z=$target_center+$cap3_z_rear 

      tubs cap4_rear \
           innerRadius=$cap4_r1 \
           outerRadius=$cap4_r2 \
           length=$cap4_l_rear \
           material=$t_shell_mat \
           color=$Target_Shell_Color \
     
      place cap4_rear \
            x=0 \
            y=0 \
            z=$target_center+$cap4_z_rear
endgroup

place proton_target \
      x=0 \
      y=0 \
      z=$Tposition \
      rotation=Y$TYangle


##********************
## Water pipes dimensions

param pipe_l=240-$cap3_r2
param pipe_r1=$cap5_r2
param pipe_r2=1.8*.5
param pipe_angle=30.0003
param L=$cap4_z-.5*$cap4_l
param R=$cap3_r2

##********************
## Holding Boxes dimensions

param Hold_color=$Green

param Hold_Box_h=10
param Hold_Box_l=20
param Hold_Box_w=10
param Hold_Box_z=$Tposition-.25*$Hold_Box_l

box Hold_Box height=$Hold_Box_h width=$Hold_Box_w length=$Hold_Box_l material=$t_shell_mat color=$Hold_color

##********************
## Target Hold Rings

param Target_Hold_r2=$CL1_inner_r
param Target_Hold_r1=$Target_Hold_r2-10
param Target_Hold_l=10.0

tubs Target_Hold innerRadius=$Target_Hold_r1 outerRadius=$Target_Hold_r2 length=$Target_Hold_l material=$t_shell_mat color=$Hold_color
place Target_Hold z=$Tposition-.75*$Hold_Box_l+.5*$Target_Hold_l

##********************
## Pipes Holding Rings

param Pipe_Hold_l=3.0
param Pipe_Hold_r1=.5*($Target_Hold_r1+$Target_Hold_r2-$Pipe_Hold_l)
param Pipe_Hold_r2=.5*($Target_Hold_r1+$Target_Hold_r2+$Pipe_Hold_l)

tubs Pipe_Hold innerRadius=$Pipe_Hold_r1 outerRadius=$Pipe_Hold_r2 length=$Pipe_Hold_l material=$t_shell_mat color=$Hold_color
place Pipe_Hold x=0 y=0 z=$Tposition-.4*($Tposition-.75*$Hold_Box_l+491.0)
place Pipe_Hold x=0 y=0 z=$Tposition-.7*($Tposition-.75*$Hold_Box_l+491.0)

##********************
## Water pipes 

do i 0 2
param Bx=$L*sin($TYangle*3.14159/180)+$R*sin(($pipe_angle+$i*120)*3.14159/180)*cos($TYangle*3.14159/180)
param By=$R*cos(($pipe_angle+$i*120)*3.14159/180)
param Bz=$L*cos($TYangle*3.14159/180)-$R*sin(($pipe_angle+$i*120)*3.14159/180)*sin($TYangle*3.14159/180)

param Cx=($Target_Hold_r1-$Hold_Box_h)*sin(($pipe_angle+$i*120)*3.14159/180)
param Cy=($Target_Hold_r1-$Hold_Box_h)*cos(($pipe_angle+$i*120)*3.14159/180)
param Cz=0


param pipe_l=sqrt(($Cx-$Bx)*($Cx-$Bx)+($Cy-$By)*($Cy-$By)+($Cz-$Bz)*($Cz-$Bz))

param pipe_l_xz=sqrt(($Cx-$Bx)*($Cx-$Bx)+($Cz-$Bz)*($Cz-$Bz))

param angle1=(asin(($Cy-$By)/$pipe_l))*180/3.14159
param angle2=(asin(($Cx-$Bx)/($pipe_l_xz)))*180/3.14159


param pipe_x1=.5*($Bx+$Cx)
param pipe_y1=.5*($By+$Cy)
param pipe_z1=.5*($Bz+$Cz)

tubs pipe$i innerRadius=$pipe_r1 outerRadius=$pipe_r2 length=$pipe_l material=$t_shell_mat color=$Target_Shell_Color
place pipe$i x=$pipe_x1 y=$pipe_y1 z=$pipe_z1+$Tposition rotation=X$angle1,Y-$angle2

##********************
## Back side

param Dx=-$L*sin($TYangle*3.14159/180)+$R*sin(($pipe_angle+180+$i*120)*3.14159/180)*cos($TYangle*3.14159/180)
param Dy=$R*cos(($pipe_angle+180+$i*120)*3.14159/180)
param Dz=-$L*cos($TYangle*3.14159/180)-$R*sin(($pipe_angle+180+$i*120)*3.14159/180)*sin($TYangle*3.14159/180)

param Fx=($Target_Hold_r1-$Hold_Box_h)*sin(($pipe_angle+180+$i*120)*3.14159/180)
param Fy=($Target_Hold_r1-$Hold_Box_h)*cos(($pipe_angle+180+$i*120)*3.14159/180)
param Fz=0

param pipe_x2=.5*($Dx+$Fx)
param pipe_y2=.5*($Dy+$Fy)
param pipe_z2=.5*($Dz+$Fz)

place pipe$i x=$pipe_x2 y=$pipe_y2 z=$pipe_z2+$Tposition rotation=X$angle1,Y-$angle2

enddo


##********************
## Horizontal target pipes and holding boxes on the holding ring 

param horizontal_pipe_l=$Tposition-.75*$Hold_Box_l+491.0
param horizontal_pipe_z=$Tposition-.75*$Hold_Box_l-.5*$horizontal_pipe_l

tubs horizontal_pipe innerRadius=$pipe_r1 outerRadius=$pipe_r2 length=$horizontal_pipe_l material=$t_shell_mat color=$Target_Shell_Color

do m o 5

param horizontal_pipe_x=.5*($Target_Hold_r1+$Target_Hold_r2)*sin(($pipe_angle+60*$m)*3.14159/180)
param horizontal_pipe_y=.5*($Target_Hold_r1+$Target_Hold_r2)*cos(($pipe_angle+60*$m)*3.14159/180)

place horizontal_pipe x=$horizontal_pipe_x y=$horizontal_pipe_y z=$horizontal_pipe_z

param Hold_Box_x=($Target_Hold_r1-.5*$Hold_Box_h)*sin(($pipe_angle+60*$m)*3.14159/180)
param Hold_Box_y=($Target_Hold_r1-.5*$Hold_Box_h)*cos(($pipe_angle+60*$m)*3.14159/180)

param Hold_Box_angle=$pipe_angle+60*$m
place Hold_Box x=$Hold_Box_x y=$Hold_Box_y z=$Hold_Box_z rotation=Z-$Hold_Box_angle

enddo

param TXstart=0.5*($Tlength)*sin($TYangle*3.14159/180.0)*cos($TXangle*3.14159/180.0)-$MECO_G4_xTrans
param TYstart=-0.5*($Tlength)*sin($TXangle*3.14159/180.0)
param TZstart=$Tposition+0.5*($Tlength)*cos($TYangle*3.14159/180.0)*cos($TXangle*3.14159/180.0)-$MECO_G4_zTrans
param TXend=-0.5*($Tlength)*sin($TYangle*3.14159/180.0)*cos($TXangle*3.14159/180.0)-$MECO_G4_xTrans
param TYend=+0.5*($Tlength)*sin($TXangle*3.14159/180.0)
param TZend=$Tposition-0.5*($Tlength)*cos($TYangle*3.14159/180.0)*cos($TXangle*3.14159/180.0)-$MECO_G4_zTrans
