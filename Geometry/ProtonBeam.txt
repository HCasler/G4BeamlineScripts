# ******************
# Proton Beamline
# ******************
# Beam line layout taken from CJJ layout of 14/02/13
# m = magnet center, e = magnet end
param FFFQ11m=2605.280
param FFFQ11e=2991.360
param FFDQ21m=5841.840
param FFDQ21e=6276.840
param FFFQ31m=10670.981
param FFFQ31e=11057.061
param FFDQ41m=17698.632
param FFDQ41e=18084.712
param FFFQ32m=24726.283
param FFFQ32e=25112.363
param FFDQ22m=29555.424
param FFDQ22e=29990.424
param FFFQ12m=32791.984
param FFFQ12e=33178.064

# End point of CJJ beam sheet ties in to the EndBeam trajectory point
# defined in GeometryParameters.txt

param FFEND=34897.264

# **************************************************************************
# Beam Start Point: This corresponds to the upstream focal point
# I adjusted this to optimize the target focus
# **************************************************************************
param -unset StartBeamL=-7650.0

param -unset StartBeamXrot=$EndBeamXrot
param -unset StartBeamYrot=$EndBeamYrot
param ctheta=cos($StartBeamXrot*degree)
param stheta=sin($StartBeamXrot*degree)
param cphi=cos($StartBeamYrot*degree)
param sphi=sin($StartBeamYrot*degree)
param cEtheta=cos($EndBeamXrot*degree)
param sEtheta=sin($EndBeamXrot*degree)

param StartBeamX=$EndBeamX+$ctheta*$sphi*($FFEND-$StartBeamL)
param StartBeamY=$EndBeamY-$stheta*($FFEND-$StartBeamL)
param StartBeamZ=$EndBeamZ+$ctheta*$cphi*($FFEND-$StartBeamL)

param Rot=x$StartBeamXrot,y$StartBeamYrot
param lenFQ1=2*($FFFQ11e-$FFFQ11m)
param lenDQ2=2*($FFDQ21e-$FFDQ21m)
param lenFQ3=2*($FFFQ31e-$FFFQ31m)
param lenDQ4=2*($FFDQ41e-$FFDQ41m)

param Qaperture=6.625*25.4/2.0

# Quad gradients proportional to CJJ k values
param ktog=29.65
param -unset FQ1g=$ktog*0.19392
param -unset DQ2g=$ktog*0.31294
param -unset FQ3g=$ktog*0.20465
param -unset DQ4g=$ktog*0.16817

genericquad FQ1 apertureRadius=$Qaperture fieldLength=$lenFQ1 \
      ironColor=1,0,0 ironRadius=300. ironLength=$lenFQ1 \
      fieldMaterial=Vacuum ironMaterial=Fe 

genericquad DQ2 apertureRadius=$Qaperture fieldLength=$lenDQ2 \
      ironColor=1,0,0 ironRadius=300. ironLength=$lenDQ2 \
      fieldMaterial=Vacuum ironMaterial=Fe 

genericquad FQ3 apertureRadius=$Qaperture fieldLength=$lenFQ3 \
      ironColor=1,0,0 ironRadius=300. ironLength=$lenFQ3 \
      fieldMaterial=Vacuum ironMaterial=Fe 

genericquad DQ4 apertureRadius=$Qaperture fieldLength=$lenDQ4 \
      ironColor=1,0,0 ironRadius=300. ironLength=$lenDQ4 \
      fieldMaterial=Vacuum ironMaterial=Fe 

param xFQ1=$EndBeamX+$ctheta*$sphi*($FFEND-$FFFQ11m)
param xDQ2=$EndBeamX+$ctheta*$sphi*($FFEND-$FFDQ21m)
param xFQ3=$EndBeamX+$ctheta*$sphi*($FFEND-$FFFQ31m)
param xDQ4=$EndBeamX+$ctheta*$sphi*($FFEND-$FFDQ41m)

param yFQ1=$EndBeamY-$stheta*($FFEND-$FFFQ11m)
param yDQ2=$EndBeamY-$stheta*($FFEND-$FFDQ21m)
param yFQ3=$EndBeamY-$stheta*($FFEND-$FFFQ31m)
param yDQ4=$EndBeamY-$stheta*($FFEND-$FFDQ41m)

param zFQ1=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFFQ11m)
param zDQ2=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFDQ21m)
param zFQ3=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFFQ31m)
param zDQ4=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFDQ41m)

place FQ1 gradient=$FQ1g z=$zFQ1 x=$xFQ1 y=$yFQ1 rotation=Z90.,$Rot
place DQ2 gradient=$DQ2g z=$zDQ2 x=$xDQ2 y=$yDQ2 rotation=$Rot
place FQ3 gradient=$FQ3g z=$zFQ3 x=$xFQ3 y=$yFQ3 rotation=Z90.,$Rot
place DQ4 gradient=$DQ4g z=$zDQ4 x=$xDQ4 y=$yDQ4 rotation=$Rot

param xFQ1=$EndBeamX+$ctheta*$sphi*($FFEND-$FFFQ12m)
param xDQ2=$EndBeamX+$ctheta*$sphi*($FFEND-$FFDQ22m)
param xFQ3=$EndBeamX+$ctheta*$sphi*($FFEND-$FFFQ32m)

param yFQ1=$EndBeamY-$stheta*($FFEND-$FFFQ12m)
param yDQ2=$EndBeamY-$stheta*($FFEND-$FFDQ22m)
param yFQ3=$EndBeamY-$stheta*($FFEND-$FFFQ32m)

param zFQ1=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFFQ12m)
param zDQ2=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFDQ22m)
param zFQ3=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFFQ32m)

place FQ1 gradient=$FQ1g z=$zFQ1 x=$xFQ1 y=$yFQ1 rotation=Z90.,$Rot
place DQ2 gradient=$DQ2g z=$zDQ2 x=$xDQ2 y=$yDQ2 rotation=$Rot
place FQ3 gradient=$FQ3g z=$zFQ3 x=$xFQ3 y=$yFQ3 rotation=Z90.,$Rot

genericbend trim ironColor=$Teal fieldMaterial=Vacuum ironMaterial=Fe \
       fieldHeight=3.25*25.4 fieldWidth=12*25.4  fieldLength=48*25.4 \
	ironHeight=12*25.4    ironWidth=24*25.4   ironLength=48*25.4

#trim names number and locations are not official
param FFH11=$FFFQ32m-36*25.4-$lenFQ3/2.0
param FFV11=$FFDQ22m-36*25.4-$lenFQ3/2.0
param FFH12=$FFFQ12m-36*25.4-$lenFQ3/2.0
param FFV12=$FFFQ12m+36*25.4+$lenFQ3/2.0

param xV11=$EndBeamX+$ctheta*$sphi*($FFEND-$FFV11)
param xH11=$EndBeamX+$ctheta*$sphi*($FFEND-$FFH11)
param xV12=$EndBeamX+$ctheta*$sphi*($FFEND-$FFV12)
param xH12=$EndBeamX+$ctheta*$sphi*($FFEND-$FFH12)

param yV11=$EndBeamY-$stheta*($FFEND-$FFV11)
param yH11=$EndBeamY-$stheta*($FFEND-$FFH11)
param yV12=$EndBeamY-$stheta*($FFEND-$FFV12)
param yH12=$EndBeamY-$stheta*($FFEND-$FFH12)

param zV11=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFV11)
param zH11=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFH11)
param zV12=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFV12)
param zH12=$EndBeamZ+$ctheta*$cphi*($FFEND-$FFH12)

param -unset V11b=0.0
param -unset H11b=0.0
param -unset V12b=0.0
param -unset H12b=0.0

place trim rename=V11 By=$V11b z=$zV11 x=$xV11 y=$yV11 rotation=Z90.,$Rot
place trim rename=H11 By=$H11b z=$zH11 x=$xH11 y=$yH11 rotation=$Rot
place trim rename=V12 By=$V12b z=$zV12 x=$xV12 y=$yV12 rotation=Z90.,$Rot
place trim rename=H12 By=$H12b z=$zH12 x=$xH12 y=$yH12 rotation=$Rot

# ***********************
#  Protection collimators
# ***********************

param BeamColl_l=1000
param BeamColl_or=250
param -unset BeamCollu_ir=40
param -unset BeamColld_ir=40
param -unset BeamCollu_d=7000
param -unset BeamColld_d=500
tubs BeamCollu material=Fe color=$Tungsten length=$BeamColl_l \
	innerRadius=$BeamCollu_ir outerRadius=$BeamColl_or
tubs BeamColld material=Fe color=$Tungsten length=$BeamColl_l \
	innerRadius=$BeamColld_ir outerRadius=$BeamColl_or

param L=$FFEND-$FFFQ11m+$lenFQ1/2+$BeamColl_l/2+$BeamCollu_d
param BCx=$EndBeamX+$ctheta*$sphi*$L
param BCy=$EndBeamY-$stheta*$L
param BCz=$EndBeamZ+$ctheta*$cphi*$L
place BeamCollu x=$BCx y=$BCy z=$BCz rotation=$Rot

param L=$BeamColl_l/2+$BeamColld_d
param BCx=$EndBeamX-$cEtheta*$sphi*$L
param BCy=$EndBeamY+$sEtheta*$L
param BCz=$EndBeamZ-$cEtheta*$cphi*$L
place BeamColld x=$BCx y=$BCy z=$BCz rotation=x$EndBeamXrot,y$EndBeamYrot
