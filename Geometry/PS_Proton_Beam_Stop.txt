# **********************************************
# Proton Beam Stop and Extinction Monitor Filter
# **********************************************
param pi=acos(-1)
param Concrete="0.55,0.55,0.45"
param DarkConcrete="0.5,0.5,0.4"
param ChannelShield=invisible

# **********************************
# Filter parameters
# **********************************

param -unset IncludeFilter=0
param -unset FilterMomentum=8000
param -unset FilterInDetector=0
param -unset FilterOutDetector=0
param -unset FilterTestGeom=0

if $IncludeFilter==1
 param filtChanW=50
 param filtChanH=50
 param filtExitW=75
 param filtExitH=75
 param filtChanSpace=25.4/2
 param filtChanRdown=7*25.4
 param filtChanRup=100
 param filtMagAW=50
 param filtMagAH=100
 param filtMagTW=190
 param filtMagTH=300
 param filtMagL=3556
 param filtMagB=0.222
 param filtBend=0.5*$filtMagB*0.2998*$filtMagL/$FilterMomentum
endif

# **********************************
# Proton beam stop parameters
# **********************************

param -unset pStop_Kill=1

param pStop_t=1000.
param pStop_h=1500.
param pStop_w=1500.
param pStop_l=2000.
param pStop_H=$pStop_h+2*$pStop_t
param pStop_W=$pStop_w+2*$pStop_t
param pStop_L=$pStop_l+3*$pStop_t

param filtUS=$pStop_l+2*$pStop_t
param filtMS=4200.
param filtDS=4000.
param filtHS=2200.

param pStop_x=-3237 
param pStop_y=528.     
param pStop_z=-11500 
param pStop_rot=13.3
param pStop_ang=$pStop_rot*$pi/180
param pStop_Cave=$pStop_t/2

if $IncludeFilter==1
  param pStop_Trgt_X=-1.0-186.7*(1000/$FilterMomentum)**1.58
  param pStop_Trgt_Y=-5.0+94.40*(1000/$FilterMomentum)**2.07
  param pStop_Trgt_Z=1764.5
  param d=sqrt(($pStop_Trgt_X-$pStop_x)**2+($pStop_Trgt_Z-$pStop_z)**2)
  param pStop_Chan_X=($pStop_Trgt_X-$d*sin($pStop_ang)-$pStop_x)*cos($pStop_ang)
  param pStop_Chan_A=atan(($pStop_y+$pStop_h/2+$pStop_t/2-$pStop_Trgt_Y)/($d-$pStop_l/2-2*$pStop_t))
  param pStop_Chan_W=2.0*($filtChanSpace+$filtChanRdown)
  param d=$pStop_t*tan($pStop_Chan_A)-$pStop_Chan_W/(2*cos($pStop_Chan_A))
  if $d<1
    param pStop_Cave=$pStop_t/2-(1-$d)
  endif
else
  param filtHS=1.
  param pStop_Trgt_X=0
  param pStop_Trgt_Y=0
  param pStop_Trgt_Z=1764.5
  param pStop_Chan_X=0
  param pStop_Chan_A=0
  param pStop_Chan_W=100
endif

# **********************************
#   Construct beam stop 
# **********************************

param psTotL=$filtUS+$filtMS+$filtDS
param psTotH=$filtHS+$pStop_H
box pStop color=invisible material=Air \
	length=$psTotL+2 height=$psTotH width=$pStop_W
param psTotx=$pStop_x-sin($pStop_ang)*($psTotL-$pStop_l-4*$pStop_t)/2.0
param psToty=$pStop_y+$filtHS/2.0
param psTotz=$pStop_z-cos($pStop_ang)*($psTotL-$pStop_l-4*$pStop_t)/2.0

# Split into upstream and downstream section
box psUp color=invisible length=$filtUS         height=$psTotH width=$pStop_W
box psDn color=invisible  length=$filtMS+$filtDS height=$psTotH width=$pStop_W

# Split upstream section into top and bottom sections

box psUT color=invisible length=$filtUS height=$filtHS+$pStop_t width=$pStop_W
box psUB color=invisible length=$filtUS height=$pStop_H-$pStop_t width=$pStop_W

# Split downstream section into top and bottom sections

box psDT length=$filtMS+$filtDS height=$filtHS width=$pStop_W \
  color=invisible material=Air
box psDB length=$filtMS+$filtDS height=$pStop_H width=$pStop_W \
  color=invisible material=Air

# Bottom of upstream section

param x=$pStop_W/2
param y=($pStop_H-$pStop_t)/2
param d=$pStop_t
extrusion psCore material=CONCRETE color=$Concrete kill=$pStop_Kill \
  length=$pStop_l \
  vertices=$x,$y;$x,-$y;-$x,-$y;-$x,$y;-$x+$d,$y;-$x+$d,-$y+$d;$x-$d,-$y+$d;$x-$d,$y

extrusion psFace material=CONCRETE color=$Concrete kill=$pStop_Kill \
  length=$pStop_t \
  vertices=$x,$y;$x,-$y;-$x,-$y;-$x,$y;-$x+$d,$y;-$x+$d,-$y+$d;$x-$d,-$y+$d;$x-$d,$y

param d=$pStop_t/2
extrusion psCave material=CONCRETE color=$DarkConcrete kill=$pStop_Kill \
  length=$pStop_t \
  vertices=$x,$y;$x,-$y;-$x,-$y;-$x,$y;-$x+$d,$y;-$x+$d,-$y+$d;$x-$d,-$y+$d;$x-$d,$y

box psUBD material=Al color=$Aluminum  \
	length=$pStop_l height=$pStop_H-$pStop_t width=$pStop_W
place psCore parent=psUBD x=0 y=0 z=0
box psUBM material=Air color=invisible  \
	length=$pStop_t height=$pStop_H-$pStop_t width=$pStop_W
place psCave parent=psUBM x=0 y=0 z=0
box psUBU material=Air color=invisible  \
	length=$pStop_t height=$pStop_H-$pStop_t width=$pStop_W
place psFace parent=psUBU x=0 y=0 z=0
place psUBD parent=psUB x=0 y=0 z=($pStop_l-$filtUS)/2
place psUBM parent=psUB x=0 y=0 z=($filtUS-$pStop_t)/2-$pStop_t
place psUBU parent=psUB x=0 y=0 z=($filtUS-$pStop_t)/2
place psUB parent=psUp x=0 y=($pStop_H-$pStop_t-$psTotH)/2 z=0

# Bottom of downstream section

box pStopBack material=CONCRETE color=$Concrete \
	length=$pStop_t height=$pStop_H width=$pStop_W
place pStopBack parent=psDB x=0 y=0 z=($filtMS+$filtDS-$pStop_t)/2

if $IncludeFilter==1
  box pStopDirt material=CONCRETE color=$DarkConcrete kill=$pStop_Kill \
	length=$filtMS+$filtDS-$pStop_t height=$pStop_H width=$pStop_W
  place pStopDirt parent=psDB x=0 y=0 z=-$pStop_t/2
endif

place psDB parent=psDn x=0 y=($pStop_H-$psTotH)/2 z=0

# Top of upstream section

param wv=$pStop_W/2+$pStop_Chan_X-$pStop_Chan_W/2-$pStop_t/2
param wi=$pStop_W/2-$pStop_Chan_X-$pStop_Chan_W/2-$pStop_t/2
param x=$filtUS/2
param xa=$x-$pStop_t
param xb=$xa-$pStop_t
param y=($filtHS+$pStop_t)/2
param ya=$pStop_Cave-$y

box psUTS material=CONCRETE length=$filtUS height=2*$y width=$pStop_t/2
box psUTV material=Air color=invisible length=$filtUS height=2*$y width=$wv
box psUTC material=Air color=invisible length=$filtUS height=2*$y width=$pStop_Chan_W
box psUTI material=Air color=invisible length=$filtUS height=2*$y width=$wi

extrusion psUTVConc material=CONCRETE color=$Concrete length=$wv \
  vertices=$x,$y;$x,-$y;$xa,-$y;$xa,$ya;$xb,$ya;$xb,-$y;-$x,-$y;-$x,$y
place psUTVConc parent=psUTV x=0 y=0 z=0 rotation=y-90

extrusion psUTIConc material=CONCRETE color=invisible length=$wi \
  vertices=$x,$y;$x,-$y;$xa,-$y;$xa,$ya;$xb,$ya;$xb,-$y;-$x,-$y;-$x,$y
place psUTIConc parent=psUTI x=0 y=0 z=0 rotation=y-90

extrusion psUTCConc material=CONCRETE color=invisible length=$pStop_Chan_W \
  vertices=$x,$y;$x,-$y;$xa,-$y;$xa,$ya;$xb,$ya;$xb,-$y;-$x,-$y;-$x,$y

# upstream filter channel

if $IncludeFilter==1
  param ya=$pStop_Chan_W/cos($pStop_Chan_A)/2-$filtUS*tan($pStop_Chan_A)/2+0.1
  param yb=$ya-$pStop_Chan_W/cos($pStop_Chan_A)-0.1
  extrusion filtUpA material=Air color=invisible length=$pStop_Chan_W \
	vertices=$x,$ya;$x,$yb;-$x,-$ya;-$x,-$yb
  param L=$filtUS/cos($pStop_Chan_A)-$pStop_Chan_W*tan($pStop_Chan_A)-0.1
  box filtUp material=CONCRETE color=invisible length=$L \
	width=$pStop_Chan_W height=$pStop_Chan_W
  tubs filtAirUa material=Air color=invisible length=$L/2 \
	innerRadius=0 outerRadius=$filtChanSpace+$filtChanRup
  tubs filtConcUa material=CONCRETE color=$DarkConcrete length=$L/2 \
	innerRadius=0 outerRadius=$filtChanRup
  tubs filtAirUb material=Air color=invisible length=$L/2 \
	innerRadius=0 outerRadius=$filtChanSpace+$filtChanRdown
  tubs filtConcUb material=CONCRETE color=$DarkConcrete length=$L/2 \
	innerRadius=0 outerRadius=$filtChanRdown
  box filtUpChan material=Air color=$Black length=$L/2 \
	height=$filtChanH width=$filtChanW
  place filtUpChan parent=filtConcUa x=0 y=0 z=0
  place filtUpChan parent=filtConcUb x=0 y=0 z=0
  place filtConcUa parent=filtAirUa x=0 y=0 z=0
  place filtConcUb parent=filtAirUb x=0 y=0 z=0
  place filtAirUa parent=filtUp x=0 y=0 z=$L/4
  place filtAirUb parent=filtUp x=0 y=0 z=-$L/4
  place filtUp parent=filtUpA rotation=x$pStop_Chan_A*180/$pi,y90 x=0 y=0 z=0
  place filtUpA parent=psUTCConc \
  	x=0 y=$filtUS*tan($pStop_Chan_A)/2-$filtHS/2 z=0
endif
place psUTCConc parent=psUTC x=0 y=0 z=0 rotation=y-90

place psUTS parent=psUT color=$DarkConcrete x=($pStop_t/2-$pStop_W)/2 y=0 z=0
place psUTV parent=psUT x=($wv+$pStop_t-$pStop_W)/2 y=0 z=0
place psUTC parent=psUT x=$pStop_Chan_X y=0 z=0
place psUTI parent=psUT x=($pStop_W-$wi-$pStop_t)/2 y=0 z=0
place psUTS parent=psUT color=invisible x=($pStop_W-$pStop_t/2)/2 y=0 z=0
place psUT parent=psUp x=0 y=($psTotH-$filtHS-$pStop_t)/2 z=0

# Top of downstream section

if $IncludeFilter==1
  param au=$pStop_Chan_A-$filtBend
  param ad=$pStop_Chan_A-2*$filtBend
  param space=($filtMS-$filtMagL*cos($au))/2
  param Yu=($filtUS+$space)*tan($pStop_Chan_A)-$pStop_t/2-$filtHS/2
  param Yd=$Yu+$filtMagL*sin($au)+$space*tan($ad)
  box psDTM material=Air color=invisible length=$filtMS height=$filtHS width=$pStop_W
  genericbend filtMag ironMaterial=Fe fieldMaterial=Air ironColor=$Teal \
	 ironWidth=$filtMagTH  ironHeight=$filtMagTW  ironLength=$filtMagL \
	fieldWidth=$filtMagAH fieldHeight=$filtMagAW fieldLength=$filtMagL \
	By=$filtMagB
  param d=($filtMagAH-$filtChanH/cos($filtBend))/2
  place filtMag parent=psDTM rotation=z-90,x$au*180/$pi \
	x=$pStop_Chan_X y=$Yu+$filtMagL*sin($au)/2+$d*cos($au) z=$d*sin($au)
  place psDTM parent=psDT x=0 y=0 z=$filtDS/2

  box psDTD material=Air color=invisible length=$filtDS height=$filtHS width=$pStop_W

  box psDTDV material=CONCRETE color=$Concrete length=$filtDS height=$filtHS \
	width=$pStop_W/2+$pStop_Chan_X-$pStop_Chan_W/2
  place psDTDV parent=psDTD x=$pStop_Chan_X/2-$pStop_Chan_W/4-$pStop_W/4 y=0 z=0

  box psDTDI material=CONCRETE color=invisible length=$filtDS height=$filtHS \
	width=$pStop_W/2-$pStop_Chan_X-$pStop_Chan_W/2
  place psDTDI parent=psDTD x=$pStop_Chan_X/2+$pStop_Chan_W/4+$pStop_W/4 y=0 z=0

  box psDTDC material=CONCRETE color=invisible length=$filtDS height=$filtHS \
  	width=$pStop_Chan_W
  param x=$filtDS/2
  param ya=$pStop_Chan_W/cos($ad)/2-$filtDS*tan($ad)/2+0.1
  param yb=$ya-$pStop_Chan_W/cos($ad)-0.1
  extrusion filtDnA material=Air color=invisible length=$pStop_Chan_W \
	vertices=$x,$ya;$x,$yb;-$x,-$ya;-$x,-$yb
  param L=$filtDS/cos($ad)-$pStop_Chan_W*tan($ad)-0.1
  box filtDn material=CONCRETE color=invisible length=$L \
	width=$pStop_Chan_W height=$pStop_Chan_W
  tubs filtAirDa material=Air color=invisible length=$L/2 \
	innerRadius=0 outerRadius=$filtChanSpace+$filtChanRup
  tubs filtConcDa material=CONCRETE color=$DarkConcrete length=$L/2 \
	innerRadius=0 outerRadius=$filtChanRup
  tubs filtAirDb material=Air color=invisible length=$L/2 \
	innerRadius=0 outerRadius=$filtChanSpace+$filtChanRdown
  tubs filtConcDb material=CONCRETE color=$DarkConcrete length=$L/2 \
	innerRadius=0 outerRadius=$filtChanRdown
  box filtDnChan material=Air color=$Black length=$L/2 \
	height=$filtChanH width=$filtChanW
  box filtDnExit material=Air color=$Black length=$L/2 \
	height=$filtExitH width=$filtExitW
  place filtDnChan parent=filtConcDa x=0 y=0 z=0
  place filtDnExit parent=filtConcDb x=0 y=0 z=0
  place filtConcDa parent=filtAirDa x=0 y=0 z=0
  place filtConcDb parent=filtAirDb x=0 y=0 z=0
  place filtAirDa parent=filtDn x=0 y=0 z=$L/4
  place filtAirDb parent=filtDn x=0 y=0 z=-$L/4
  place filtDn parent=filtDnA rotation=x$ad*180/$pi,y90 x=0 y=0 z=0
  place filtDnA parent=psDTDC x=0 y=$Yd+$filtDS*tan($ad)/2 z=0 rotation=y-90

  place psDTDC parent=psDTD x=$pStop_Chan_X y=0 z=0

  place psDTD parent=psDT x=0 y=0 z=-$filtMS/2

  place psDT parent=psDn x=0 y=($psTotH-$filtHS)/2 z=0
endif

# Put sections together

place psUp parent=pStop x=0 y=0 z=($filtMS+$filtDS)/2
place psDn parent=pStop x=0 y=0 z=-$filtUS/2

# **********************************
#   Add virtual detectors 
# **********************************
if $FilterInDetector==1
  virtualdetector FiltIn height=$pStop_H width=$pStop_W length=1
  place FiltIn parent=pStop x=0 y=$pStop_H/2-$psTotH/2 z=$psTotL/2+0.5
endif
if $FilterOutDetector==1
  virtualdetector FiltOut height=$filtHS width=$pStop_W length=1
  place FiltOut parent=pStop x=0 y=$psTotH/2-$filtHS/2 z=-$psTotL/2-0.5
endif
if $FilterInDetector>1
  virtualdetector FiltIn height=$FilterInDetector width=$FilterInDetector length=1
  place FiltIn parent=pStop x=$pStop_Chan_X z=$psTotL/2+0.5 \
	y=$pStop_H-$pStop_t/2-$psTotH/2
endif
if ($FilterOutDetector>1)&&($IncludeFilter==1)
  virtualdetector FiltOut height=$FilterOutDetector width=$FilterOutDetector length=1
  place FiltOut parent=pStop x=$pStop_Chan_X z=-$psTotL/2-0.5 \
	y=$pStop_H+$filtHS/2+$Yd+$filtDS*tan($ad)-$psTotH/2
endif

place pStop x=$psTotx y=$psToty z=$psTotz rotation=y$pStop_rot

# **********************************
#   Air gap between solenoid and beam stop 
# **********************************

param dx=0.5*$pStop_w+$pStop_t
param dz=0.5*$pStop_l+2*$pStop_t+1.1
param x1=$pStop_x-$dx*cos($pStop_ang)+$dz*sin($pStop_ang)
param z1=$pStop_z+$dx*sin($pStop_ang)+$dz*cos($pStop_ang)
param x2=$pStop_x+$dx*cos($pStop_ang)+$dz*sin($pStop_ang)
param z2=$pStop_z-$dx*sin($pStop_ang)+$dz*cos($pStop_ang)
param x3=1000
param z3=-536.5
extrusion AirGap material=Air color=invisible length=$pStop_H \
       vertices=$x1,$z1;$x2,$z2;$x3,$z3;-$x3,$z3
place AirGap x=0 z=0 y=$pStop_y rotation=x90

# **********************************
#   Test beam points to channel location 
# **********************************

if $FilterTestGeom==1
  beam gaussian particle=proton nEvents=$Num_Events firstEvent=$First_Event \
              beamX=$pStop_Trgt_X beamY=$pStop_Trgt_Y beamZ=$pStop_Trgt_Z \
              sigmaX=0 sigmaY=0 sigmaXp=0.0 sigmaYp=0.0 \
              meanMomentum=$FilterMomentum sigmaP=0.0 \
              meanT=0.0 sigmaT=0.0 \
              rotation=x-$pStop_Chan_A*180/$pi,y180+$pStop_rot
endif
