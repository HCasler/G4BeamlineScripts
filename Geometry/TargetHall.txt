# ********************************************
#   Air volume and kill zones between solenoid and beam stop 
# ********************************************
param -unset TargetHall_color=invisible
param x1=$TargetHall_x1
param z1=$TargetHall_z1
param x2=$TargetHall_x2
param z2=$TargetHall_z2
param x3=$TargetHall_x3
param z3=$TargetHall_z3
param cs=cos($PBSenclosure_yrot*degree)
param sn=sin($PBSenclosure_yrot*degree)
param dx=$PBSenclosure_width/2
param dz=$PBSenclosure_length/2+1
param xa=$PBSenclosure_x+$dx*$cs+$dz*$sn
param za=$PBSenclosure_z-$dx*$sn+$dz*$cs
param xb=$PBSenclosure_x-$dx*$cs+$dz*$sn
param zb=$PBSenclosure_z+$dx*$sn+$dz*$cs
param z5=$PScryostat_z+$PScryostat_Offset-$PSendcap_length-$PSendcap_winT
extrusion AirGap material=$TargetHall_material color=$TargetHall_color \
	length=$TargetHall_h \
	vertices=$x1,$z1;$x2,$z2;$xa,$za;$xb,$zb;$x3,$z3;$x3,$z5;$x1,$z5

# top and bottom kill zones
param -unset Wall_Kill=1
extrusion AirKilla material=$TargetHall_material color=invisible \
	kill=$Wall_Kill length=1 \
	vertices=$x1,$z1;$x2,$z2;$xa,$za;$xb,$zb;$x3,$z3;$x3,$z5;$x1,$z5
place AirKilla parent=AirGap x=0 y=0 z=$TargetHall_h/2-0.5
place AirKilla parent=AirGap x=0 y=0 z=-$TargetHall_h/2+0.5

# side kill zones
extrusion AirKillb material=$TargetHall_material color=invisible \
	kill=$Wall_Kill length=$TargetHall_h-2.0 \
	vertices=$x1,$z1;$x2,$z2;$x2,$z2+1;$x1-1,$z2+1;$x1-1,$z5;$x1,$z5
place AirKillb parent=AirGap x=0 y=0 z=0.0
extrusion AirKillc material=$TargetHall_material color=invisible \
	kill=$Wall_Kill length=$TargetHall_h-2.0 \
	vertices=$xb,$zb;$x3,$z3;$x3,$z5;$x3+1,$z5;$x3+1,$z3;$xb+1,$zb
place AirKillc parent=AirGap x=0 y=0 z=0.0

place AirGap x=0 z=0 y=$TargetHall_yfloor+$TargetHall_h/2 rotation=x90
